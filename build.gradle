buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
    }
}

ext.configFile = file "build.properties"

ext.configFile.withReader {
    def prop = new Properties()
    prop.load(it)
    ext.config = new ConfigSlurper().parse prop
}

apply plugin: "forge"

version = "${config.minecraft_version}-${config.mod_version}.${config.build_number}"
group = config.group
archivesBaseName = config.archivesBaseName

minecraft {
    version = "${config.minecraft_version}-${config.forge_version}"

    replace "@VERSION@", "${project.version}"
}

dependencies {
    testCompile "junit:junit:4.11"
    compile "com.esotericsoftware.reflectasm:reflectasm:${config.reflectasm_version}"
    compile "org.xerial:sqlite-jdbc:${config.sqlite_version}"
    compile "mysql:mysql-connector-java:${config.mysql_version}"
    compile project(":MyEssentials-Core")
}

processResources {
    inputs.property "mod_version", project.version
    inputs.property "minecraft_version", project.minecraft.version
    inputs.property "reflectasm_version", config.reflectasm_version
    inputs.property "mysql_version", config.mysql_version
    inputs.property "sqlite_version", config.sqlite_version

    from(sourceSets.main.resources.srcDirs) {
        include '*.info'
        expand 'mod_version': project.version, 'minecraft_version': project.minecraft.version, 'reflectasm_version': config.reflectasm_version, 'mysql_version': config.mysql_version, 'sqlite_version': config.sqlite_version
    }
    from(sourceSets.main.resources.srcDirs) {
        exclude '*.info'
    }
}