buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
    }
}

ext.configFile = file "build.properties"

ext.configFile.withReader {
    def prop = new Properties()
    prop.load(it)
    ext.config = new ConfigSlurper().parse prop
}

apply plugin: "forge"

version = "${config.minecraft_version}-${config.mod_version}.${config.build_number}"
group = config.group
archivesBaseName = config.archivesBaseName

minecraft {
    version = "${config.minecraft_version}-${config.forge_version}"

    replace "@VERSION@", "${project.version}"
}

dependencies {
    testCompile "junit:junit:4.11"
    compile "com.esotericsoftware.reflectasm:reflectasm:1.09"
    // Include DB Drivers as deps
    compile "org.xerial:sqlite-jdbc:3.7.15-M1"
    compile "mysql:mysql-connector-java:5.1.31"
    compile project(":MyEssentials-Core")
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'mod_version': project.version, 'minecraft_version': project.minecraft.version
    }
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}